// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.3.1'
    compile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.2'
    compile group: 'io.rest-assured', name: 'json-schema-validator', version: '3.0.2'
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'

    compile group: 'org.testng', name: 'testng', version: '6.11'
}

// Checkstyle configuration
checkstyle {
    toolVersion = '7.6'
}

// FindBugs configuration
findbugs {
    toolVersion = "3.0.1"
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// PMD Configuration
pmd {
    toolVersion = '5.5.1'
    ruleSets = ["java-basic"]
}
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

test {
    useTestNG()
    systemProperty 'browser', browser

    scanForTestClasses false

    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
}
